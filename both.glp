#set term qt size 1000,700

set term dumb
pi = 4.*atan(1.)

MT = 0.10
MP = (1-2*MT)/2.0
set rmargin 0.5
#set bmargin 2


set xr [0:up]
set xlabel "{Energy (GeV)}"
set xtics 0, up / 10, up
set mxtics 5
set format x "%g"


set yr [*:*]
plot dataset u 1:5 w histe, \
     ''      u 1:6 w histe, \
     ''      u 1:7 w histe, \
     ''      u 1:8 w histe

y1min = GPVAL_Y_MIN
y1max = GPVAL_Y_MAX

set yr [*:*]
plot dataset u 1:2 w histe

y2min = GPVAL_Y_MIN
y2max = GPVAL_Y_MAX


#set term epslatex color size 5in, 2.5in lw 2 dashed
set term cairolatex pdf color size 5in, 2.5in lw 2 dashed
#set term x11 size 1000, 500 dashed
set output dataset.".tex"

#dashed blue
set style line 1 lt 2 lw 2 lc rgb "blue" 
#solid blue
set style line 2 lt 1 lw 2 lc rgb "blue" 
#solid red
set style line 3 lt 1 lw 2 lc rgb "red" 
#dashed red
set style line 4 lt 2 lw 2 lc rgb "red" 

set multiplot

#plot res
set lmargin at screen MT
set bmargin at screen 1.5*MT
set tmargin 0
set origin 0, 0
set size 1, MT+MP

set ylabel "{Ratio to nominal}"
set ytics y1min-1, (y1max-y1min) / 4, y1max+(y1max-y1min)*0.99
set mytics 5
set format y "%g"
#set label 1 "HK DR" center at 0, 100

set key top right samplen 1 Left invert at screen 1.5, screen 1.5
#set yr [0.75 < * < 0.9:1.1 < * < 1.25]
set yr [y1min - (y1max-y1min)*0.1: y1max + (y1max-y1min)*0.1]
plot dataset u 1:5 w histe ls 1 t " ", \
     ''      u 1:6 w histe ls 2 t " ", \
     ''      u 1:7 w histe ls 3 t " ", \
     ''      u 1:8 w histe ls 4 t " ", \
     1 w histe lc rgb "black" not


#plot  full hist

set lmargin at screen MT
set bmargin 0
set tmargin 0.5
set origin 0, MT+MP
set size 1, MT+MP

set xlabel ""
set format x ""

set ylabel "{Events}"
set format y "%g"
set ytics 0, (y2max) / 5, y2max
set mytics 5

set key default
set key top right samplen 1.5 Left invert
set yr [0: y2max + (y2max-y2min)*0.05]
plot dataset u 1:2 w histe lw 2 lc rgb "black" not, \
     '+' u 1:(NaN) w histe ls 1 t "\$-3\\sigma\$", \
     dataset u 1:3 w histe ls 2 t "\$-1\\sigma\$", \
     dataset u 1:4 w histe ls 3 t "\$+1\\sigma\$", \
     '+' u 1:(NaN) w histe ls 4 t "\$+3\\sigma\$"

unset multiplot
unset output
